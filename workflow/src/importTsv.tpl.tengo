
ll := import("@platforma-sdk/workflow-tengo:ll")
self := import("@platforma-sdk/workflow-tengo:tpl")
exec := import("@platforma-sdk/workflow-tengo:exec")
assets := import("@platforma-sdk/workflow-tengo:assets")
pframes := import("@platforma-sdk/workflow-tengo:pframes")
pColumn := import("@platforma-sdk/workflow-tengo:pframes.pcolumn")
pSpec := import("@platforma-sdk/workflow-tengo:pframes.spec")
xsv := import("@platforma-sdk/workflow-tengo:pframes.xsv")
text := import("text")
json := import("json")

self.defineOutputs("vUsage", "jUsage", "vFamilyUsage", "jFamilyUsage", "isotypeUsage")

tsvImportParams := func(usageType, tracing){

	// gene/isotype axis spec
	axisSpec:= undefined
	// column spec
	columnSpec:= undefined

	if (usageType == "isotypeUsage") {
		axisSpec = {
			name: "pl7.app/vdj/isotype",
			type: "String",
			annotations: {
				"pl7.app/label": "Isotype"
			}
		}

		columnSpec = {
			name: "pl7.app/vdj/isotypeUsage",
			valueType: "Float",
			annotations: {
				"pl7.app/trace": tracing,
				"pl7.app/label": "Isotype usage"
			}
		}
	} else {
		geneType := text.to_upper(usageType[0:1]) // first letter is either V or J
		geneNameFormat := "gene"
		if text.contains(usageType, "Family") {
			geneNameFormat = "family"
		}

		axisSpec = {
			name: "pl7.app/vdj/gene",
			type: "String",
			domain: {
				"pl7.app/vdj/geneType": geneType,
				"pl7.app/vdj/geneNameFormat": geneNameFormat
			},
			annotations: {
				"pl7.app/label": geneType + " " + geneNameFormat
			}
		}

		columnSpec =  {
			name: "pl7.app/vdj/geneUsage",
			valueType: "Float",
			domain: {
				"pl7.app/vdj/geneType": geneType,
				"pl7.app/vdj/geneNameFormat": geneNameFormat
			},
			annotations: {
				"pl7.app/trace": tracing,
				"pl7.app/label": geneType + " " + geneNameFormat + " usage"
			}
		}
	}

	return {
		"axes": [
			{
				column: "sample",
				preProcess: [{
					type: "regexpReplace",
					pattern: "^(.*)\\.clns$",
					replacement: "$1"
				}]
				/* spec: sampleId spec must be set outside */
			},
			{
				column: "metric",
				spec: axisSpec
			}
			],
		columns: [
			{
				column: "value",
				id: "value",
				allowNA: true,
				spec: columnSpec
			}
			],
		storageFormat: "Binary",
		partitionKeyLength: 0
	}
}


FILE_NAME_PREFIX_LEN := {
	"vUsage" : len("r.vUsage."),
	"jUsage" : len("r.jUsage."),
	"vFamilyUsage" : len("r.vFamilyUsage."),
	"jFamilyUsage" : len("r.jFamilyUsage."),
	"isotypeUsage" : len("r.IsotypeUsage.")
}
FILE_NAME_SUFFIX_LEN := len(".tsv")


importUsage := func(tsv, usageType, sampleIdAxisSpec, tracing) {

	tsvMap := pColumn.resourceMapBuilder(/* key length*/ 1 )
	for fileName, file in tsv {
		chain := fileName[FILE_NAME_PREFIX_LEN[usageType]:(len(fileName) - FILE_NAME_SUFFIX_LEN)]
		tsvMap.add([chain], file)
	}
	tsvMap = tsvMap.build()

	spec := tsvImportParams(usageType, tracing)
	spec.axes[0].spec = sampleIdAxisSpec

	pf := xsv.importFileMap(tsvMap, "tsv", spec, {
		additionalAxesSpec: [
			{
				"name": "pl7.app/vdj/chain",
				"type": "String",
				"annotations": {
					"pl7.app/label": "Chain",
					"pl7.app/axisNature": "scaleCompatible"
				}
			}
			],
		transpose: {
			pAxisIdx: 0,
			pAxisNameOverride: "sample",
			sAxisName: "metric",
			valueName: "value"
		}
	})

	return pf
}

self.body(func(args) {
	inputSpec := args.inputSpec
	blockId := args.blockId

	trace := pSpec.makeTrace(inputSpec,
		//{type: "milaboratories.repertoire-gene-usage.filtering", id: blockId + ".filtering", importance: 30, label: filteringStr},
		{type: "milaboratories.repertoire-gene-usage", id: blockId, importance: 25, label: "Repertoire Gene Usage"}
	)

	return {
		vUsage: importUsage(args.vUsageTsv, "vUsage", inputSpec.axesSpec[0], trace.valueStr),
		jUsage: importUsage(args.jUsageTsv, "jUsage", inputSpec.axesSpec[0], trace.valueStr),
		vFamilyUsage: importUsage(args.vFamilyUsageTsv, "vFamilyUsage", inputSpec.axesSpec[0], trace.valueStr),
		jFamilyUsage: importUsage(args.jFamilyUsageTsv, "jFamilyUsage", inputSpec.axesSpec[0], trace.valueStr),
		isotypeUsage: importUsage(args.isotypeUsageTsv, "isotypeUsage", inputSpec.axesSpec[0], trace.valueStr)
	}
})
