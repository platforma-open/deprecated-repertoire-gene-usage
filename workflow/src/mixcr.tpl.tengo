// mixcr postanalysis individual # gene usage

self := import("@platforma-sdk/workflow-tengo:tpl")
exec := import("@platforma-sdk/workflow-tengo:exec")
assets := import("@platforma-sdk/workflow-tengo:assets")
times := import("times")
json := import("json")

mixcrSw := assets.importSoftware("@platforma-open/milaboratories.software-mixcr:main")

self.defineOutputs(["vUsageTsv", "jUsageTsv", "vFamilyUsageTsv", "jFamilyUsageTsv", "isotypeUsageTsv"])

self.body(func(args) {
	clns := args.clns.data

	filesMap := {}
	for k, v in clns.inputs() {
		filesMap[json.decode(k)[0]] = v
	}

	cmdBuilder := exec.builder().
		software(mixcrSw).
		arg("postanalysis").
		arg("individual").
		arg("--default-downsampling").arg(args.downsampling).
		arg("--default-weight-function").arg(args.weight).
		secret("MI_LICENSE", "MI_LICENSE")

	if args.dropOutliers == true {
		cmdBuilder = cmdBuilder.arg("--drop-outliers")
	}

	if args.onlyProductive == true {
		cmdBuilder = cmdBuilder.arg("--only-productive")
	}

	for name, f in filesMap {
		cmdBuilder = cmdBuilder.arg(name + ".clns")
	}
	cmdBuilder = cmdBuilder.arg("r.json")

	for name, f in filesMap {
		cmdBuilder = cmdBuilder.addFile(name + ".clns", f)
	}

	cmdBuilder = cmdBuilder.saveFileSet("vUsageTsv", "^r\\.vUsage\\..*\\.tsv$").
		saveFileSet("jUsageTsv", "^r\\.jUsage\\..*\\.tsv$").
		saveFileSet("vFamilyUsageTsv", "^r\\.vFamilyUsage\\..*\\.tsv$").
		saveFileSet("jFamilyUsageTsv", "^r\\.jFamilyUsage\\..*\\.tsv$").
		saveFileSet("isotypeUsageTsv", "^r\\.IsotypeUsage\\..*\\.tsv$")

	cmdBuilder = cmdBuilder.cache(48 * times.hour);

	run := cmdBuilder.run()

	return {
		vUsageTsv: run.getFileSet("vUsageTsv"),
		jUsageTsv: run.getFileSet("jUsageTsv"),
		vFamilyUsageTsv: run.getFileSet("vFamilyUsageTsv"),
		jFamilyUsageTsv: run.getFileSet("jFamilyUsageTsv"),
		isotypeUsageTsv: run.getFileSet("isotypeUsageTsv")
	}
})
